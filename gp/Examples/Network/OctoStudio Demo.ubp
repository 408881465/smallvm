module main
author wwj
version 1 0 
description ''
variables count _OctoScanning shape 

script 306 40 {
comment 'OctoStudio on Android phones is not very stable. 
After running for some time, 
the beam and wait for blocks might not work. 
If you''re not receiving messages, 
try restarting the OctoStudio  APP.'
}

script 56 59 {
whenStarted
'Octo start'
count = 0
forever {
  shape = ('Octo receive beam')
  if (shape != (booleanConstant false)) {
    count += 1
    sayIt shape count
  }
  waitMillis 100
}
}

script 342 193 {
'beam to phones' 'heart'
}


module OctoStudio Comm
author wwj718
version 1 0 
choices shape_choices triangle circle square heart star 
description 'Communicate with OctoStudio (https://octostudio.org/)'
variables _OctoScanning 

  spec ' ' 'Octo start' 'Octo start'
  spec ' ' 'beam to phones' 'Octo beam to phones _' 'menu.shape_choices' 'heart'
  spec 'r' 'Octo receive beam' 'Octo receive beam'
  spec ' ' 'Octo stop' 'Octo stop'
  spec 'r' '_generate_full_shape_id' '_generate_full_shape_id shape _' 'auto' 'heart'
  spec ' ' '_Octo deinit' '_Octo deinit'
  spec 'r' '_shape_map' '_shape_map'

to 'Octo receive beam' {
  local 'shape_id' (callCustomReporter '[net:OctoGetOctoShapeId]')
  if (shape_id != (booleanConstant false)) {
    for s ('_shape_map') {
      if ((at 2 s) == shape_id) {return (at 1 s)}
    }
  } else {
    return (booleanConstant false)
  }
}

to 'Octo start' {
  if (not (callCustomReporter '[net:OctoGetBLEInitialized]')) {callCustomCommand '[net:OctoInitBLE]'}
  if (_OctoScanning == 0) {callCustomCommand '[net:OctoStartScanning]'}
  _OctoScanning = 1
}

to 'Octo stop' {
  if (_OctoScanning == 1) {
    callCustomCommand '[net:OctoStopScanning]'
    _OctoScanning = 0
  }
}

to '_Octo deinit' {
  callCustomCommand '[net:OctoDeinitBLE]'
}

to '_generate_full_shape_id' shape_name {
  local 'shape_id' -1
  for s ('_shape_map') {
    if ((at 1 s) == shape_name) {return ('[data:asByteArray]' ('[data:makeList]' (random 0 255) (random 0 255) (random 0 255) (random 0 255) 0 0 0 (at 2 s) 0 0 0 0 0))}
  }
  return 'This shape does not exist'
}

to '_shape_map' {
  comment '"0": "circle",
"1": "square",
"2": "star",
"3": "heart",
"4": "triangle",'
  return ('[data:makeList]' ('[data:makeList]' 'circle' 0) ('[data:makeList]' 'square' 1) ('[data:makeList]' 'star' 2) ('[data:makeList]' 'heart' 3) ('[data:makeList]' 'triangle' 4))
}

to 'beam to phones' shape {
  if (callCustomReporter '[net:OctoGetBLEInitialized]') {
    callCustomCommand '[net:OctoSetAdvertisementData]' ('[data:makeList]' ('_generate_full_shape_id' shape))
    callCustomCommand '[net:OctoStartAdvertising]'
    waitMillis 200
    callCustomCommand '[net:OctoStopAdvertising]'
  } else {
    sayIt 'Please start Octo first'
  }
}

