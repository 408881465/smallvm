module main
variables humidity temperature

script 54 50 {
whenStarted
'wifi connect to' 'Network_Name' '' 3
defineThing 'Temperature' 'TemperatureSensor'
addNumProp 'Temperature' 'temperature' -40 100 'TemperatureProperty'
addNumProp 'Humidity' 'humidity' 0 100 'LevelProperty'
forever {
  temperature = (temperature_DHT11 25)
  waitMillis 2000
  humidity = (humidity_DHT11 25)
  waitMillis 2000
}
}

script 471 225 {
comment 'test temp'
forever {
  sayIt (temperature_DHT11 25)
  waitMillis 2000
}
}

script 471 350 {
comment 'test humidity'
forever {
  sayIt (humidity_DHT11 25)
  waitMillis 2000
}
}

script 53 360 {
comment 'add this once the VM supports it'
'_addCustomProperty' 'unit' '"percent"'
}

script 52 434 ('thing description')


module DHT Input
variables _dht_temperature _dht_humidity _dhtData _dhtLastReadTime

	spec 'r' 'temperature_DHT11' 'temperature (Celsius) DHT11 pin _' 'auto' 4
	spec 'r' 'humidity_DHT11' 'humidity DHT11 pin _' 'auto' 4
	spec 'r' 'temperature_DHT22' 'temperature (Celsius) DHT22 _' 'auto' 4
	spec 'r' 'humidity_DHT22' 'humidity DHT22 pin _' 'auto' 4
	spec ' ' '_dhtReadData' '_dhtReadData pin _' 'auto any' '10'
	spec 'r' '_dhtChecksumOkay' '_dhtChecksumOkay' 'any'
	spec ' ' '_dhtUpdate' '_dhtUpdate _ isDHT11 _' 'auto bool any' 4 true
	spec 'r' '_dhtReady' '_dhtReady' 'any'

to '_dhtChecksumOkay' {
  local 'checksum' 0
  for i 4 {
    checksum += (at i _dhtData)
  }
  checksum = (checksum & 255)
  return (checksum == (at 5 _dhtData))
}

to '_dhtReadData' pin {
  comment 'Create DHT data array the first time'
  if (_dhtData == 0) {
    _dhtData = (newList 5)
  }
  comment 'Pull pin low for >18msec to request data'
  digitalWriteOp pin false
  waitMillis 20
  comment 'Read DHT start pulses (H L H L)'
  waitUntil (digitalReadOp pin)
  waitUntil (not (digitalReadOp pin))
  waitUntil (digitalReadOp pin)
  waitUntil (not (digitalReadOp pin))
  local 'i' 1
  local 'byte' 0
  local 'bit' 1
  comment 'Read 40 bits (5 bytes)'
  repeat 40 {
    waitUntil (digitalReadOp pin)
    local 'start' (microsOp)
    waitUntil (not (digitalReadOp pin))
    if (((microsOp) - start) > 40) {
      comment 'Long pulse - append a "1" bit'
      byte += 1
    }
    if (bit == 8) {
      atPut i _dhtData byte
      i += 1
      byte = 0
      bit = 1
    } else {
      byte = (byte << 1)
      bit += 1
    }
    waitUntil (not (digitalReadOp pin))
  }
}

to '_dhtReady' {
  local 'elapsed' ((millisOp) - _dhtLastReadTime)
  return (or (elapsed < 0) (elapsed > 2000))
}

to '_dhtUpdate' pin isDHT11 {
  if ('_dhtReady') {
    '_dhtReadData' pin
    _dhtLastReadTime = (millisOp)
  }
  if ('_dhtChecksumOkay') {
    if isDHT11 {
      _dht_temperature = (at 3 _dhtData)
      _dht_humidity = (at 1 _dhtData)
    } else {
      local 'n' (((at 1 _dhtData) * 256) + (at 2 _dhtData))
      _dht_humidity = ((n + 5) / 10)
      n = ((((at 3 _dhtData) & 127) * 256) + (at 4 _dhtData))
      if (((at 3 _dhtData) & 128) != 0) {
        n = (0 - n)
      }
      _dht_temperature = ((n + 5) / 10)
    }
  }
}

to humidity_DHT11 pin {
  '_dhtUpdate' pin true
  return _dht_humidity
}

to humidity_DHT22 pin {
  '_dhtUpdate' pin false
  return _dht_humidity
}

to temperature_DHT11 pin {
  '_dhtUpdate' pin true
  return _dht_temperature
}

to temperature_DHT22 pin {
  '_dhtUpdate' pin false
  return _dht_temperature
}


module 'Web Things' Comm
variables _WoT_definingEvents '_thing description'

	spec ' ' 'wifi connect to' 'wifi connect to _ password _ try _ times' 'str str num' 'Network_Name' '' 3
	spec ' ' 'wifiCreateHotspot' 'wifi create hotspot _ password _' 'str str' 'Network_Name' 'Network_Password'
	spec 'r' 'getIPAddress' 'IP address'
	spec ' ' 'defineThing' 'define thing _ capability _' 'str str.thingTypesMenu' 'Hello LED' 'Light'
	spec ' ' 'registerEvent' 'register event _ type _' 'str str.thingEventTypesMenu' 'Button Pressed' 'PressedEvent'
	spec ' ' 'addBooleanProperty' 'add boolean property title _ variable _ @Type _' 'str menu.allVarsMenu str.booleanPropertyTypesMenu' '' '' 'OnOffProperty'
	spec ' ' 'addNumProp' 'add number property title _ variable _ min _ max _ @Type _' 'str menu.allVarsMenu num num str.numberPropertyTypesMenu' '' '' 0 100 'LevelProperty'
	spec ' ' 'addStringProperty' 'add string property title _ variable _ @Type _' 'str menu.allVarsMenu str.stringPropertyTypesMenu' '' '' 'ColorProperty'
	spec ' ' '_addCustomProperty' '_add to last property key _ value _' 'str auto' '' '"json value"'
	spec ' ' 'append to thing description' 'append to thing description _' 'auto' ''
	spec ' ' 'clear thing description' 'clear thing description'
	spec 'r' 'thing description' 'thing description'

to '_addCustomProperty' key jsonValue {
  'append to thing description' ('[data:join]' '"' key '": ' jsonValue)
}

to addBooleanProperty title var typeName {
  'append to thing description' ('[data:join]' '    "' var '": {"title": "' title '", "type": "boolean", "href": "/properties/' var '", "@type": "' typeName '"},')
}

to addNumProp title var min max typeName {
  'append to thing description' ('[data:join]' '    "' var '": {"title": "' title '", "type": "number", "minimum": ' min ', "maximum": ' max ', "href": "/properties/' var '", "@type": "' typeName '"')
  if ('TemperatureProperty' == typeName) {
    'append to thing description' ', "unit": "degree celsius"'
  }
  'append to thing description' '},'
}

to addStringProperty title var typeName {
  'append to thing description' ('[data:join]' '    "' var '": {"title": "' title '", "type": "string", "href": "/properties/' var '", "@type": "' typeName '"},')
}

to 'append to thing description' 'more JSON' {
  if ((v '_thing description') == 0) {'clear thing description'}
  '_thing description' = ('[data:join]' (v '_thing description') (v 'more JSON'))
}

to 'clear thing description' {
  '_thing description' = ''
}

to defineThing name capability {
  _WoT_definingEvents = (booleanConstant false)
  'clear thing description'
  if ('' == capability) {
    'append to thing description' ('[data:join]' '{ "title": "' name '",
  "@type": [ ],
  "links":[{
    "rel": "events",
    "href": "/events"
   }],
   "properties": {')
  } else {
    'append to thing description' ('[data:join]' '{ "title": "' name '",
  "@context": "https://iot.mozilla.org/schemas/",
  "@type": ["' capability '"],
  "links":[{
    "rel": "events",
    "href": "/events"
   }],
  "properties": {')
  }
}

to getIPAddress {
  return ('[net:myIPAddress]')
}

to registerEvent title type {
  if (not _WoT_definingEvents) {
    local 'last char' (at (size (v '_thing description')) (v '_thing description'))
    if ((v 'last char') == '{') {
      comment 'thing description ends with "{", meaning there are no properties'
      'append to thing description' '},"events":{'
    } ((v 'last char') == ',') {
      comment 'thing description ends with ",", meaning there''s at least one property'
      '_thing description' = ('[data:join]' ('[data:copyFromTo]' (v '_thing description') 1 ((size (v '_thing description')) - 1)) '},"events":{')
    }
    _WoT_definingEvents = (booleanConstant true)
  }
  'append to thing description' ('[data:join]' '    "' title '": {"description":"MicroBlocks event", "@type":"' type '"},')
}

to 'thing description' {
  if ((v '_thing description') == 0) {return 'thing description is empty'}
  local 'description' (v '_thing description')
  comment 'Fix incomplete thing descriptions'
  if ((at 'last' (v '_thing description')) == ',') {
    comment 'close last property / event'
    description = ('[data:join]' ('[data:copyFromTo]' (v '_thing description') 1 ((size (v '_thing description')) - 1)) '}}')
  } ((at 'last' (v '_thing description')) == '{') {
    comment 'no properties'
    description = ('[data:join]' (v '_thing description') '}}')
  }
  return description
}

to 'wifi connect to' ssid password tries {
  if (not ('[net:hasWiFi]')) {return}
  tries += -1
  '[net:startWiFi]' ssid password
  repeatUntil (or ('Connected' == ('[net:wifiStatus]')) (tries < 1)) {
    '[net:startWiFi]' ssid password
    repeatUntil ('Trying...' != ('[net:wifiStatus]')) {
      comment 'Slow blink while connecting'
      setUserLED true
      waitMillis 500
      setUserLED false
      waitMillis 500
    }
    tries += -1
  }
  repeat 8 {
    comment 'Quick blinks when connected'
    setUserLED true
    waitMillis 50
    setUserLED false
    waitMillis 50
  }
  sayIt 'My IP address is:' ('[net:myIPAddress]')
}

to wifiCreateHotspot ssid password {
  if (not ('[net:hasWiFi]')) {return}
  '[net:startWiFi]' ssid password true
  repeatUntil ('Connected' == ('[net:wifiStatus]')) {
    comment 'Slow blink while connecting'
    setUserLED true
    waitMillis 500
    setUserLED false
    waitMillis 500
  }
  repeat 8 {
    comment 'Quick blinks when connected'
    setUserLED true
    waitMillis 50
    setUserLED false
    waitMillis 50
  }
  sayIt 'My IP address is:' ('[net:myIPAddress]')
}

