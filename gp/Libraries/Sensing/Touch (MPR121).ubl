module 'Touch (MPR121)' Input
author MicroBlocks
version 1 5 
description 'Support for the MPR121 12-channel i2c touch sensor.'
variables _touch_initialized _touch_state _touch_last_state _touch_queue _touch_release_queue _touch_last_touched_pin _touch_last_released_pin 

  spec 'r' 'touch_isTouched' 'is pin _ touched?' 'menu.range:1-12' 1
  space
  spec 'r' 'touch_touch_event' 'pin touch event'
  spec 'r' 'touch_last_touched_pin' 'last touched pin'
  space
  spec 'r' 'touch_release_event' 'pin release event'
  spec 'r' 'touch_last_released_pin' 'last release pin'
  space
  spec 'r' 'touch_state_string' 'touch state string'
  space
  spec ' ' '_touch_init' '_touch_init'
  spec ' ' '_touch_record_changed_pins' '_touch_record_changed_pins'
  spec ' ' '_touch_update' '_touch_update'
  spec ' ' '_touch_setRegister' '_touch_setRegister _ to _' 'auto auto' 'hex register' 'hex value'

to '_touch_init' {
  comment 'Create touch and release queues.'
  if (_touch_queue == 0) {
    _touch_queue = ('[data:makeList]')
  }
  if (_touch_release_queue == 0) {
    _touch_release_queue = ('[data:makeList]')
  }
  comment 'Soft reset -- sets all registers to defaults'
  '_touch_setRegister' '0x80' '0x63'
  comment 'Set filter parameters'
  '_touch_setRegister' '0x2B' '0x01'
  '_touch_setRegister' '0x2C' '0x01'
  '_touch_setRegister' '0x2D' '0x10'
  '_touch_setRegister' '0x2E' '0x20'
  '_touch_setRegister' '0x2F' '0x01'
  '_touch_setRegister' '0x30' '0x01'
  '_touch_setRegister' '0x31' '0x10'
  '_touch_setRegister' '0x32' '0x20'
  '_touch_setRegister' '0x33' '0x01'
  '_touch_setRegister' '0x34' '0x10'
  '_touch_setRegister' '0x35' '0xFF'
  comment 'Init debounce and config registers'
  '_touch_setRegister' '0x5B' '0x11'
  '_touch_setRegister' '0x5C' '0xFF'
  '_touch_setRegister' '0x5D' '0x30'
  comment 'Set touch/release thresholds'
  local 'touchThreshold' 40
  local 'releaseThreshold' 20
  for i 12 {
    local 'reg' ((hexToInt '40') + (2 * i))
    i2cSet 90 (reg - 1) touchThreshold
    i2cSet 90 reg releaseThreshold
  }
  comment 'Start tracking all 12 inputs (proximity detection disabled)'
  '_touch_setRegister' '0x5E' '0xCC'
  _touch_initialized = (booleanConstant true)
}

to '_touch_record_changed_pins' {
  local 'last_state' _touch_last_state
  '_touch_update'
  comment 'If a pin was just touched or released, return the index of that pin,
otherwise, return zero.'
  for i 12 {
    local 'previous' (last_state & (1 << (i - 1)))
    local 'current' (_touch_state & (1 << (i - 1)))
    if (and (previous == 0) (current != 0)) {
      if (('[data:find]' i _touch_queue) < 0) {
        comment 'Record the index of the newly touched pin'
        '[data:addLast]' i _touch_queue
      }
    }
    if (and (previous != 0) (current == 0)) {
      if (('[data:find]' i _touch_release_queue) < 0) {
        comment 'Record the index of the newly released pin'
        '[data:addLast]' i _touch_release_queue
      }
    }
  }
  _touch_last_state = _touch_state
}

to '_touch_setRegister' reg value {
  i2cSet 90 (hexToInt reg) (hexToInt value)
}

to '_touch_update' {
  if (not _touch_initialized) {'_touch_init'}
  _touch_state = (((i2cGet 90 1) << 8) | (i2cGet 90 0))
  if (_touch_state < 0) {
    _touch_state = 0
  }
}

to touch_isTouched pin {
  if (or (pin < 1) (pin > 12)) {return (booleanConstant false)}
  '_touch_update'
  return ((_touch_state & (1 << (pin - 1))) != 0)
}

to touch_last_released_pin {
  comment 'Return the pin that triggered the last release event.'
  return _touch_last_released_pin
}

to touch_last_touched_pin {
  comment 'Return the pin that triggered the last touch event.'
  return _touch_last_touched_pin
}

to touch_release_event {
  if (not _touch_initialized) {'_touch_init'}
  if ((size _touch_release_queue) == 0) {'_touch_record_changed_pins'}
  if ((size _touch_release_queue) > 0) {
    _touch_last_released_pin = (at 1 _touch_release_queue)
    '[data:delete]' 1 _touch_release_queue
    return (booleanConstant true)
  }
  _touch_last_released_pin = -1
  return (booleanConstant false)
}

to touch_state_string {
  '_touch_update'
  local 'pin states' ('[data:makeList]')
  for i 12 {
    if ((_touch_state & (1 << (i - 1))) != 0) {
      '[data:addLast]' '1' (v 'pin states')
    } else {
      '[data:addLast]' '0' (v 'pin states')
    }
  }
  return ('[data:joinStrings]' (v 'pin states'))
}

to touch_touch_event {
  if (not _touch_initialized) {'_touch_init'}
  if ((size _touch_queue) == 0) {'_touch_record_changed_pins'}
  if ((size _touch_queue) > 0) {
    _touch_last_touched_pin = (at 1 _touch_queue)
    '[data:delete]' 1 _touch_queue
    return (booleanConstant true)
  }
  _touch_last_touched_pin = -1
  return (booleanConstant false)
}

