sharedVariables '_dhtData' '_dht_temperature' '_dht_humidity' '_dhtLastReadTime'

spec ' ' '_dht read data' '_dht read data pin _' 'auto any' '10' nil
spec 'r' '_dhtChecksumOkay' '_dhtChecksumOkay' 'any' nil
spec 'r' 'temperature_DHT' 'temperature (Celsius) DHT11 pin _' 'auto' 4
spec 'r' 'humidity DHT' 'humidity DHT11 pin _' 'auto' 4
spec 'r' 'temperature C DHT' 'temperature (Celsius) DHT22 _' 'auto' 4
spec 'r' 'humidity DHT2' 'humidity DHT22 pin _' 'auto' 4
spec ' ' '_dhtReadData' '_dhtReadData _ isDHT11 _' 'auto bool any' 4 true nil
spec 'r' '_dhtReady' '_dhtReady' 'any' nil

to '_dht read data' pin {
  comment 'Create DHT data array the first time'
  if (_dhtData == 0) {
    _dhtData = (newArray 5)
  }
  comment 'Pull pin low for >18msec to request data'
  digitalWriteOp pin false
  waitMillis 20
  comment 'Read DHT start pulses (H L H L)'
  waitUntil (digitalReadOp pin)
  waitUntil (not (digitalReadOp pin))
  waitUntil (digitalReadOp pin)
  waitUntil (not (digitalReadOp pin))
  local 'i' 1
  local 'byte' 0
  local 'bit' 1
  comment 'Read 40 bits (5 bytes)'
  repeat 40 {
    waitUntil (digitalReadOp pin)
    local 'start' (microsOp)
    waitUntil (not (digitalReadOp pin))
    if (((microsOp) - start) > 40) {
      comment 'Long pulse - append a "1" bit'
      byte += 1
    }
    if (bit == 8) {
      atPut i _dhtData byte
      i += 1
      byte = 0
      bit = 1
    } else {
      byte = (byte << 1)
      bit += 1
    }
    waitUntil (not (digitalReadOp pin))
  }
}

to '_dhtChecksumOkay' {
  local 'checksum' 0
  for i 4 {
    checksum += (at i _dhtData)
  }
  checksum = (checksum & 255)
  return (checksum == (at 5 _dhtData))
}

to temperature_DHT pin {
  '_dhtReadData' pin true
  return _dht_temperature
}

to 'humidity DHT' pin {
  '_dhtReadData' pin true
  return _dht_humidity
}

to 'temperature C DHT' pin {
  '_dhtReadData' pin false
  return _dht_temperature
}

to 'humidity DHT2' pin {
  '_dhtReadData' pin false
  return _dht_humidity
}

to '_dhtReadData' pin isDHT11 {
  if ('_dhtReady') {
    '_dht read data' pin
    _dhtLastReadTime = (millisOp)
  }
  if ('_dhtChecksumOkay') {
    if isDHT11 {
      _dht_temperature = (at 3 _dhtData)
      _dht_humidity = (at 1 _dhtData)
    } else {
      local 'n' (((at 1 _dhtData) * 256) + (at 2 _dhtData))
      _dht_humidity = ((n + 5) / 10)
      n = ((((at 3 _dhtData) & 127) * 256) + (at 4 _dhtData))
      if (((at 3 _dhtData) & 128) != 0) {
        n = (0 - n)
      }
      _dht_temperature = ((n + 5) / 10)
    }
  }
}

to '_dhtReady' {
  local 'elapsed' ((millisOp) - _dhtLastReadTime)
  return (or (elapsed < 0) (elapsed > 2000))
}

