module 'Air Pressure (LPS22HB)'
author MicroBlocks
version 1 1
choices lps22hb_pressureUnits mBar kPa 'inHg (x10)' 'psi (x10)'
description 'Sense air pressure and altitude with the LPS22HB air pressure sensor.
The altimeter can either report changes relative to the starting altitude. If calibrated to a known starting altitude, it can report absolute altitude.'
variables _lps22hb_basePressure _lps22hb_baseAltitude

  spec 'r' 'lps22hb_airPressureMbar' 'air pressure (mBar)'
  spec 'r' 'lps22hb_airPressureInchesHG' 'air pressure (inches HG x10)'
  spec 'r' '_lps22hb_airPressure' 'air pressure,  _' 'menu.lps22hb_pressureUnits' 'mBar'
  space
  spec ' ' 'lps22hb_setBaseAltitude' 'set base altitude _ meters' 'num' 0
  spec 'r' 'lps22hb_altitudeMeters' 'altitude (meters)'
  spec 'r' 'lps22hb_altitudeFeet' 'altitude (feet)'
  space
  spec 'r' 'lps22hb_temperature' 'lps22hb temperature (x10)'
  space
  spec 'r' '_lps22hb_altitudeMillimeters' '_lps22hb_altitudeMillimeters'
  spec 'r' '_lps22hb_readPressure' '_lps22hb_readPressure'

to '_lps22hb_airPressure' units {
  local 'raw' ('_lps22hb_rawPressure')
  if ('kPa' == units) {
    return (raw / 40960)
  } ('mBar' == units) {
    return (raw / 4096)
  } ('inHg (x10)' == units) {
    return (raw / 13871)
  } ('psi (x10)' == units) {
    return (raw / 28241)
  }
  return (raw / 4096)
}

to '_lps22hb_altitudeMillimeters' {
  if (_lps22hb_basePressure == 0) {
    comment 'if base altitude not set, use zero (useful for relative measurments)' ,
    lps22hb_setBaseAltitude 0
  }
  mmChange = ('[misc:pressureToAltitude]' _lps22hb_basePressure ('_lps22hb_rawPressure'))
  return ((1000 * baseMeters) + mmChange)
}

to '_lps22hb_rawPressure' {
  local 'LPS22HB' (hexToInt '5C')
  local 'raw' ('_lps22hb_readPressure')
  if (raw == 0) {
    comment 'LPS22HB sometimes gets stuck returning zero pressure
This sequence may unstick it.'
    i2cSet LPS22HB (hexToInt '11') 128
    i2cSet LPS22HB (hexToInt '10') 0
    raw = ('_lps22hb_readPressure')
  }
  return raw
}

to '_lps22hb_readPressure' {
  local 'LPS22HB' (hexToInt '5C')
  i2cSet LPS22HB (hexToInt '11') 1
  waitMillis 1
  local 'result' (i2cGet LPS22HB (hexToInt '28'))
  result = (((i2cGet LPS22HB (hexToInt '29')) << 8) | result)
  result = (((i2cGet LPS22HB (hexToInt '2A')) << 16) | result)
  return result
}

to lps22hb_airPressureInchesHG {
  return (('_lps22hb_rawPressure') / 13871)
}

to lps22hb_airPressureMbar {
  return (('_lps22hb_rawPressure') / 4096)
}

to lps22hb_altitudeFeet {
  return (('_lps22hb_altitudeMillimeters') / 305)
}

to lps22hb_altitudeMeters {
  return (('_lps22hb_altitudeMillimeters') / 1000)
}

to lps22hb_setBaseAltitude baseMeters {
  _lps22hb_basePressure = ('_lps22hb_rawPressure')
  _lps22hb_baseAltitude = baseMeters
}

to lps22hb_temperature {
  local 'LPS22HB' (hexToInt '5C')
  i2cSet LPS22HB (hexToInt '11') 1
  waitMillis 1
  local 'result' (i2cGet LPS22HB (hexToInt '2B'))
  result = (((i2cGet LPS22HB (hexToInt '2C')) << 8) | result)
  if (result >= 32768) {
    result = (result - 65536)
  }
  return (result / 10)
}
