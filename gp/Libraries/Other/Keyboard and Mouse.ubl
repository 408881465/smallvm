module 'Keyboard and Mouse' Comm
author MicroBlocks
version 1 0 
description 'HID library. Emulates mouse and keyboard actions.

It supports the Adafruit boards Trinket M0 and Metro M0 Express.'

choices keyModifiers 'Shift' 'Control' 'Alt / ⌥' 'Logo / ⌘' 'AltGr'
choices mouseButtons left right middle both

  spec ' ' 'press key' 'press key _ : while holding _' 'auto menu.keyModifiers' 'n' 'Shift'
  spec ' ' 'mouse click' '_ mouse click' 'menu.mouseButtons' 'left'
  spec ' ' 'mouse move' 'move mouse pointer by _ , _' 'num num' 10 -20
  spec ' ' 'mouse wheel' 'scroll mouse by _' 'num' -5
  spec ' ' 'mouse hold' 'hold _ mouse button' 'menu.mouseButtons' 'left'
  spec ' ' 'mouse release' 'release mouse buttons'

to 'press key' key modifier {
  callCustomCommand '[hid:pressKey]' ('[data:makeList]' key ('[data:find]' modifier ('[data:makeList]' 'Shift' 'Control' 'Alt / ⌥' 'Logo / ⌘' 'AltGr')))
}

to 'mouse click' which {
  callCustomCommand '[hid:mousePress]' ('[data:makeList]' ('[data:find]' which ('[data:makeList]' 'left' 'right' 'both' 'middle')))
  waitMillis 50
  callCustomCommand '[hid:mouseRelease]'
}

to 'mouse move' x y {
  callCustomCommand '[hid:mouseMove]' ('[data:makeList]' x y)
}

to 'mouse wheel' delta {
  callCustomCommand '[hid:mouseScroll]' ('[data:makeList]' delta)
}

to 'mouse hold' which {
  callCustomCommand '[hid:mousePress]' ('[data:makeList]' ('[data:find]' which ('[data:makeList]' 'left' 'right' 'both' 'middle')))
}

to 'mouse release' {
  callCustomCommand '[hid:mouseRelease]'
}
