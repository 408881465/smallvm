module 'Encoder Motor' Output
author MicroBlocks
version 1 0 
depends Wukong 
description 'Control motors with digital encoder outputs using the Wukong board.
Pins 1 and 2 are the encoder inputs for the two motors.'
variables _encoderMotor_M1_count _encoderMotor_M2_count 

  spec ' ' '_encodeMotor_runM1' '_encodeMotor_runM1'
  spec ' ' '_encodeMotor_runM2' '_encodeMotor_runM2'
  spec ' ' 'encodeMotor_stepMotors' 'step motor 1 _ motor 2 _ power _' 'num num num' 500 500 50

to '_encodeMotor_runM1' {
  repeat _encoderMotor_M1_count {
    waitUntil (digitalReadOp 1)
    waitUntil (not (digitalReadOp 1))
  }
  wukong_setMotor 1 0
  _encoderMotor_M1_count = 0
}

to '_encodeMotor_runM2' {
  repeat _encoderMotor_M2_count {
    waitUntil (digitalReadOp 2)
    waitUntil (not (digitalReadOp 2))
  }
  wukong_setMotor 2 0
  _encoderMotor_M2_count = 0
}

to encodeMotor_stepMotors steps1 steps2 power {
  _encoderMotor_M1_count = (absoluteValue steps1)
  _encoderMotor_M2_count = (absoluteValue steps2)
  local 'pwr1' power
  if (steps1 < 0) {
    pwr1 = (0 - pwr1)
  }
  local 'pwr2' power
  if (steps2 < 0) {
    pwr2 = (0 - pwr2)
  }
  wukong_setMotor 1 pwr1
  wukong_setMotor 2 pwr2
  sendBroadcast '_encodeMotor_runM1'
  sendBroadcast '_encodeMotor_runM2'
  waitUntil (and (_encoderMotor_M1_count <= 0) (_encoderMotor_M2_count <= 0))
}

